module csvmanager

import io/filesystem
import io/error
import string
import list
import src/utils

// CSV Format: ID,FEN,MOVES,RATING,THEMES,GAMEURL
// Example: 00001,r1bqkb1r/pppp1ppp/2n2n2/4p3/2B1P3/5N2/PPPP1PPP/RNBQK2R w KQkq - 4 4,f3e5 d8h4,1500,pin discovery attack,https://lichess.org/...
// But we only really need FEN and MOVES for the current implementation


// Effect for handling invalid puzzle scenarios (file access, format issues)
effect InvalidPuzzle(msg: String): (String, List[String])

// Fallback puzzle in case of errors
val defaultPuzzle = ("8/3N2k1/5b2/5R2/r5KP/8/8/8 w - - 3 48", ["g4h5", "a4h4"])


// Gets a random puzzle from the CSV file
// Parameters:
//   path: Path to puzzle CSV file
//   deletePuzzle: If true, removes selected puzzle from file to avoid repetition
// Returns: Tuple of (FEN position, List of solution moves)
def getRandomPuzzle(path: String, deletePuzzle: Bool): (String, List[String]) / InvalidPuzzle = {

  with on[IOError].default { 
    do InvalidPuzzle("Could not read puzzle file")
    defaultPuzzle 
  }
  with on[OutOfBounds].default {
    do InvalidPuzzle("Invalid puzzle format")
    defaultPuzzle
  }

  // reading file
  val fileContent = readFile(path)
  val lines = fileContent.split("\n")
  
  // Skip header row and get random line
  val header = lines.get(0)
  val puzzles = lines.drop(1)
  val randomIndex = floor(random() * puzzles.size.toDouble) 
  
  // select random puzzle
  val selectedLine = puzzles.get(randomIndex)

  def buildNewList(index: Int, acc: List[String]): List[String] = {
    if (index >= puzzles.size) {
      acc.reverse
    } else if (index == randomIndex) {
      buildNewList(index + 1, acc)
    } else {
      buildNewList(index + 1, Cons(puzzles.get(index), acc))
    }
  }

  // setting in main menu - deletes the played puzzle if checkted
  if (deletePuzzle) {
    val remainingPuzzles = buildNewList(0, Nil())
    val newContent = header ++ "\n" ++ remainingPuzzles.join("\n")
    writeFile(path, newContent)
  }

  // parsing selected puzzle
  val fields = selectedLine.split(",")
  val movesString = fields.get(2)
  val movesArray = movesString.split(" ") 
  
  
  (fields.get(1), movesArray) // Return (FEN, Moves)

}
  

